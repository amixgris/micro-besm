шифр 419999 зс5^
лен 41(2048)^
eeв1а3
*name macro50
*no list
*call putflag*
61
*assembler
 program:       ,name,

с   Печатаем текстовую метку
            14  ,vtm,   label
                ,its,   14
            14  ,utm,   1
                ,its,   14
            14  ,vtm,   0
                ,its,   14
                ,call,  print8

с   Печатаем начальный адрес
            14  ,vtm,   macro50
                ,ita,   14
                ,atx,   buf
            14  ,vtm,   buf
                ,its,   14
                ,its,   14
            14  ,vtm,   =i1
                ,its,   14
            14  ,vtm,   =i16
                ,its,   14
                ,call,  printo

с   Печатаем длину
            14  ,vtm,   *end
                ,ita,   14
                ,ntr,   3
                ,a-x,   buf
                ,atx,   buf
            14  ,vtm,   buf
                ,its,   14
                ,its,   14
            14  ,vtm,   =i1
                ,its,   14
            14  ,vtm,   =i16
                ,its,   14
                ,call,  printo

с   Печатаем команды в восьмеричном виде
            14  ,vtm,   macro50
                ,its,   14
            14  ,vtm,   *end - 1
                ,its,   14
            14  ,vtm,   =i4
                ,its,   14
            14  ,vtm,   =i16
                ,its,   14
                ,call,  printo

с   Заканчиваем
                ,call,  stop*

 buf:           ,log,
 label:         ,iso,   12h---start---

c   Составлена А.И.Салтыковым (ОИЯИ)
c   с участием Л.Г.Каминского (ИФВЭ)
c   Версия 15.01.1975
c   Коэффициенты полиномов взяты
c   из книги 'Computer Approximations'
c   (авторы J.F.Hart и дp., издательство
c   John Wiley and Sons, New York, 1968)

 macro50:       ,entry,
 macro50:       ,ntr,
            14  ,uj,    *+3

 *ret:      13  ,uj,    . сюда помещаем команду возврата

 *fatal:        ,z33,   . сюда помещаем команду останова

 :              ,uj,    *sqrt
 :              ,uj,    *sin
 :              ,uj,    *cos
 :              ,uj,    *atan
 :              ,uj,    *asin
 :              ,uj,    *alog
 :              ,uj,    *exp
 :              ,uj,    *entier

c   Вычисление sqrt(x)
c   Среднее время счёта = 60 мксек.
c   Макс. отн. погр. =1.5e-12
c   Корень извлекается точно из всех
c   x = n**2, гдe 1 <= n < 2**20

 *sqrt:         ,bss,
            14  ,vtm,   *ret
                ,e+n,   64
 *sqr:          ,bss,
                ,atx,   x
                ,ntr,   4
            14  ,uza,
                ,ntr,   16
                ,u1a,   *errsqrt
                ,xta,   x
                ,e-x,   x
                ,atx,   y
                ,a*x,   sq2
                ,a+x,   sq1
                ,a*x,   y
                ,a+x,   sq0
                ,e+n,   64+32
                ,atx,   r
                ,xta,   x
                ,asn,   64+1
                ,atx,   z
                ,xta,   r
                ,e+x,   z
                ,atx,   y
                ,xta,   z
                ,ntr,   18
                ,uza,   *sq1
                ,xta,   y
                ,a*x,   sqrt2
                ,atx,   y
 *sq1:          ,xta,   x
                ,a/x,   y
                ,a+x,   y
                ,e+n,   64-1
                ,atx,   y
                ,xta,   x
                ,a/x,   y
                ,a+x,   y
                ,e+n,   64-1
            14  ,uj,
 *errsqrt:      ,bss,                   .TODO
                ,uj,    *fatal

c   Вычисление sin(x)
c   Среднее время счёта = 55 мксек.
c   Макс. отн. погр. = 4.e-12
c   Всегда abs(sin(x)) <= 1
c   Всегда sin(x)**2 + cos(x)**2 <= 1

 *sin:          ,bss,
                ,atx,   x
                ,avx,   x
                ,a*x,   *1/pi
                ,ntr,   10
                ,u1a,   *s3
 *s1:           ,bss,
                ,ntr,   3
                ,a+x,   int0
                ,ntr,   18
                ,atx,   y
                ,yta,   64-40
                ,atx,   z
                ,xta,   y
                ,asn,   64+8
                ,yta,
                ,atx,   y
                ,xta,   z
                ,ntr,   10
            14  ,v1m,   *s4
                ,x-a,   =r.5
                ,uj,    *s2
 *s4:           ,bss,
                ,u1a,   *s2
                ,x-a,   real1
 *s2:           ,bss,
                ,avx,   y
 *s3:           ,bss,
                ,e+n,   64+1
                ,atx,   z
                ,a*x,   z
                ,atx,   z**2
            14  ,vtm,   -5
                ,xta,   sin6
 *sinl:         ,a*x,   z**2
            14  ,a+x,   sin0
            14  ,vlm,   *sinl
                ,a*x,   z
                ,avx,   x
                ,uj,    *ret

c   Вычисление cos(x)
c   Среднее время счёта = 55 мксек.
c   Макс. отн. погр. = 4.e-12
c   Всегда abs(cos(x)) <= 1

 *cos:          ,bss,
                ,atx,   x
                ,avx,   x
                ,atx,   x
                ,a*x,   *1/pi
            14  ,vtm,
                ,uj,    *s1

c   Вычисление atan(x)
c   Среднее время счёта = 90 мксек.
c   Макс. отн. погр. = 7.e-12

 *atan:         ,bss,
                ,atx,   x
                ,avx,   x
                ,atx,   z
                ,xta,
                ,atx,   y
                ,xta,   z
                ,e+n,   64-1
                ,u1a,   *at1
                ,xta,   real1
                ,a/x,   z
                ,atx,   z
                ,xta,   pi/2
                ,atx,   y
 *at1:          ,xta,   z
                ,a*x,   z
                ,atx,   z**2
            14  ,vtm,   -2
 *qat:          ,bss,
            14  ,a+x,   qatan1
                ,a*x,   z**2
            14  ,vlm,   *qat
                ,a+x,   qatan0
                ,atx,   r
            14  ,vtm,   -3
                ,xta,   patan4
 *pat:          ,bss,
                ,a*x,   z**2
            14  ,a+x,   patan0
            14  ,vlm,   *pat
                ,a/x,   r
                ,a*x,   z
                ,a-x,   y
                ,amx,
                ,avx,   x
                ,uj,    *ret

c   Вычисление asin(x)
c   Среднее время счёта = 80 мксек.
c   Макс. отн. погр. = 7.e-12

 *asin:         ,bss,
                ,atx,   x
                ,atx,   arg
                ,avx,   x
                ,atx,   z
            14  ,vtm,   bit48
                ,ntr,   8
                ,u1a,   *as1
                ,x-a,   real1
                ,u1a,   *errasin
                ,e+n,   64-1
            14  ,vjm,   *sqr
                ,atx,   z
            14  ,vtm,   pi/2
 *as1:          ,bss,
            14  ,xta,
                ,atx,   arcsin
                ,xta,   z
                ,a*x,   z
                ,atx,   z**2
            14  ,vtm,   -2
 *qas:          ,bss,
            14  ,a+x,   qasin1
                ,a*x,   z**2
            14  ,vlm,   *qas
                ,a+x,   qasin0
                ,atx,   r
            14  ,vtm,   -2
                ,xta,   pasin3
 *pas:          ,bss,
                ,a*x,   z**2
            14  ,a+x,   pasin0
            14  ,vlm,   *pas
                ,a/x,   r
                ,a*x,   z
                ,e+x,   arcsin
                ,a-x,   arcsin
                ,amx,
                ,avx,   arg
                ,uj,    *ret
 *errasin:      ,bss,                   .TODO
                ,uj,    *fatal

c   Вычисление alog(x)
c   Среднее время счёта = 60 мксек.
c   Макс. отн. погр. = 3.e-12
c   alog(1.) в точности равен 0.

 *alog:         ,bss,
                ,avx,   sin1
                ,uza,   *errlog
                ,atx,   x
                ,e-x,   x
                ,atx,   y
                ,a-x,   sqrt2/2
                ,atx,   z
                ,a+x,   sqrt2
                ,a/x,   z
                ,atx,   z
                ,a*x,   z
                ,atx,   z**2
            14  ,vtm,   -4
                ,xta,   alog5
                ,ntr,   2
 *alogl:        ,bss,
                ,a*x,   z**2
            14  ,a+x,   alog0
            14  ,vlm,   *alogl
                ,a*x,   z
                ,a-x,   ln2/2
                ,atx,   r
                ,xta,   x
                ,aax,   =7740 0000 0000 0000
                ,asn,   64+7
                ,aex,   =4320 0000 0000 0000
                ,a*x,   ln2
                ,a+x,   r
                ,uj,    *ret
 *errlog:       ,bss,                   .TODO
                ,uj,    *fatal

c   Вычисление exp(x)
c   Среднее время счёта = 65 мксек.
c   Макс. отн. погр. = 5.e-12

 *exp:          ,bss,
                ,a*x,   *1/ln2
                ,atx,   x
                ,a-x,   =r63.
                ,uza,   *errexp
                ,a+x,   =r127.
                ,u1a,   *czero
                ,xta,   x
                ,ntr,   3
                ,a+x,   int0
                ,atx,   z
                ,ntr,
                ,yta,   64-41
                ,e+n,   64
                ,atx,   y
                ,xta,   z
                ,asn,   64+7
                ,yta,
                ,aex,   bit48
                ,atx,   z
            14  ,vtm,   -6
                ,xta,   exp7
 *expl:         ,a*x,   y
            14  ,a+x,   exp0
            14  ,vlm,   *expl
                ,atx,   r
                ,a*x,   r
                ,e+x,   z
                ,uj,    *ret
 *czero:        ,xta,
                ,uj,    *ret
 *errexp:       ,bss,                   .TODO
                ,uj,    *fatal

c   Вычисление целой части x
c   Среднее время счёта = 25 мксек.

 *entier:       ,bss,
                ,ntr,   3
                ,a+x,   int0
                ,ntr,
                ,e+n,   64
                ,uj,    *ret

 bit48:         ,log,   40000000 00000000
 int0:          ,log,   64000000 00000000
 sqrt2:         ,log,   40532404 74631772
 sqrt2/2:       ,log,   40132404 74631772
 ln2:           ,log,   40130562 05773722
 ln2/2:         ,log,   37530562 05773721
 *1/ln2:        ,log,   40534252 16624534
 *1/pi:         ,log,   37521371 40667117

c  Коэффициенты для sqrt (Computer Approximations, таблица 130)
 sq2:           ,log,   37227260 16750030
 sq1:           ,log,   40161743 60527114
 sq0:           ,log,   37520213 67076726

c  Коэффициенты для sin (Computer Approximations, таблица 3343)
 sin6:          ,log,   24165166 15135220
 sin5:          ,log,   27207264 06013034
 sin4:          ,log,   32124073 37647661
 sin3:          ,log,   34663226 46644717
 sin2:          ,log,   36521465 70633344
 sin1:          ,log,   40255242 06147334
 sin0:          ,bss,
 pi/2:          ,log,   40544417 66521041

c  Коэффициенты для atan (Computer Approximations, таблица 5096)
 patan4:        ,log,   37107122 37733234
 patan3:        ,log,   41570706 20065256
 patan2:        ,log,   43156767 61443655
 patan1:        ,log,   43566662 34335452
 patan0:        ,log,   43512633 71314703
 qatan3:        ,log,   42513705 34014651
 qatan2:        ,log,   43530352 73613334
 qatan1:        ,log,   44107710 42346022
 qatan0:        ,log,   43512633 71314705

c  Коэффициенты для asin (Computer Approximations, таблица 4696)
 pasin3:        ,log,   42620677 67311156
 pasin2:        ,log,   44171167 03564207
 pasin1:        ,log,   45275105 13075667
 pasin0:        ,log,   44524054 06731770
 qasin3:        ,log,   43231635 53476645
 qasin2:        ,log,   44516736 07622544
 qasin1:        ,log,   45266101 37701013
 qasin0:        ,log,   44524054 06731770

c  Коэффициенты для alog (Computer Approximations, таблица 2664)
 alog5:         ,log,   37145022 40420440
 alog4:         ,log,   37161365 65743216
 alog3:         ,log,   37511112 40046421
 alog2:         ,log,   37546314 62345725
 alog1:         ,log,   40125252 52526026
 alog0:         ,log,   41100000 00000000

c  Коэффициенты для exp (Computer Approximations, таблица 1024)
 exp7:          ,log,   30514007 77605675
 exp6:          ,log,   32117406 71257531
 exp5:          ,log,   33527345 60343024
 exp4:          ,log,   35116624 30023033
 exp3:          ,log,   36161530 23515710
 exp2:          ,log,   37172775 73756445
 exp1:          ,log,   40130562 05774000
 real1:         ,bss,
 exp0:          ,log,   40500000 00000000

 arcsin:        ,bss,
 x:             ,log,
 y:             ,log,
 z:             ,log,
 z**2:          ,log,
 r:             ,log,
 arg:           ,log,

 *end:          ,bss,
                ,end,

*       system load list
*execute
*end file
``````
еконец
